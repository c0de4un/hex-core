# = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# CMake
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

include( "cmake/constants.cmake" )
cmake_minimum_required( VERSION ${HEX_CMAKE_VERSION} )

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# CONFIGS
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

include( "cmake/platform.cmake" )
include( "cmake/build_types.cmake" )

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# PROJECT
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

project( "hexEngine" VERSION ${HEX_CORE_VERSION} LANGUAGES C CXX )

# Enable Position Independent Code
set( CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON )
set( CMAKE_POSITION_INDEPENDENT_CODE ON )

# C++ Version
set ( CMAKE_CXX_STANDARD ${HEX_CXX_VERSION} )
set( CMAKE_CXX_STANDARD_REQUIRED ON )
set( CMAKE_AUTOUIC ON )
set( CMAKE_AUTOMOC ON )
set( CMAKE_AUTORCC ON )

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# GLOBALS
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

include( "cmake/globals.cmake" )

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# CONSTANTS
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

set( HEX_CORE_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src" )

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# HEADERS
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

set( HEX_CORE_PUBLIC "${HEX_CORE_SRC}/public/hex/core/" )
set( HEX_CORE_HEADERS
    # CFG
    "${HEX_CORE_PUBLIC}cfg/hex_api.hpp"
    "${HEX_CORE_PUBLIC}cfg/hex_platform.hpp"
    # PCH
    "${HEX_CORE_PUBLIC}hex_core.hpp"
)

# Append to Exportable Headers
set( HEX_EXPORT_HEADERS ${HEX_CORE_HEADERS} ${HEX_EXPORT_HEADERS} )

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# SOURCES
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

set( HEX_CORE_PRIVATE "${HEX_CORE_SRC}/private/hex/core/" )
set( HEX_CORE_SOURCES
    # PCH
    "${HEX_CORE_PRIVATE}hex_core.cpp"
)

# Append to Exportable Sources
set( HEX_EXPORT_SOURCES ${HEX_CORE_SOURCES} ${HEX_EXPORT_SOURCES} )

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# MODULES
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

foreach( HEX_MODULE_NAME ${HEX_MODULES_LIST} )
    message( STATUS "${PROJECT_NAME} - including module #${HEX_MODULE_NAME}" )

    include( "${HEX_SRC_ROOT_DIR}/${HEX_MODULE_NAME}/module.cmake" )
endforeach()

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# BUILD
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

if ( HEX_CORE_BUILD_MODE STREQUAL "STATIC" )
    # Create STATIC Library Object
    add_library( hex STATIC ${HEX_EXPORT_SOURCES} ${HEX_EXPORT_HEADERS} )

    message( STATUS "${PROJECT_NAME} - building as STATIC library" )
elseif ( HEX_CORE_BUILD_MODE STREQUAL "SHARED" )
    # Create SHARED Library Object
    add_library( hex SHARED ${HEX_EXPORT_SOURCES} ${HEX_EXPORT_HEADERS} )

    message( STATUS "${PROJECT_NAME} - building as SHARED library" )
elseif ( HEX_CORE_BUILD_MODE STREQUAL "EXPORT" )
    message( STATUS "${PROJECT_NAME} - exported to <HEX_EXPORT_HEADERS> and <HEX_EXPORT_SOURCES>" )
else ( HEX_CORE_BUILD_MODE STREQUAL "STATIC" )
    message( FATAL_ERROR "${PROJECT_NAME} - undefined build-mode, configuration required" )
endif ( HEX_CORE_BUILD_MODE STREQUAL "STATIC" )

# = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =
